# tclunqlite.test --
#
#	Tests for tclunqlite
#------------------------------------------------------------------------------

lappend auto_path .
if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2
    namespace import -force ::tcltest::*
}
loadTestedCommands
package require unqlite

#-------------------------------------------------------------------------------

test unqlite-1.1 {create a database, wrong # args} {*}{
    -body {
	unqlite ::db
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test unqlite-1.2 {create a database, wrong # args} {*}{
    -body {
	unqlite ::db ":mem:" -in-memory
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test unqlite-1.3 {create a database, successful} {*}{
    -body {
        catch {
            unqlite ::db ":mem:" -in-memory 1
        } result    
    }
    -result {0}
}

test unqlite-1.4 {database config, wrong # args} {*}{
    -body {
        ::db config
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test unqlite-1.5 {database config, wrong # args} {*}{
    -body {
        ::db config -disableautocommit
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test unqlite-1.6 {database config} {*}{
    -body {
        catch {
            ::db config -disableautocommit 0
        } result
    }
    -result {0}
}

#-------------------------------------------------------------------------------

test unqlite-2.1 {Put} {*}{
    -body {
        set key "1234"
        set value "567890"

        ::db kv_store $key $value   
    }
    -result {1}
}


test unqlite-2.2 {Get} {*}{
    -body {
        ::db kv_fetch "1234"   
    }
    -result {567890}
}

test unqlite-2.3 {Initial cursor} {*}{
    -body {
        ::db cursor_init cursor1
        cursor1 first
    }
    -result {1}
}

test unqlite-2.4 {Cursor seek, wrong # args} {*}{
    -body {
        cursor1 seek
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test unqlite-2.5 {Cursor seek, wrong # args} {*}{
    -body {
        cursor1 seek "1234"
    }
    -returnCodes error
    -match glob
    -result {wrong # args*}
}

test unqlite-2.6 {Cursor seek, expected interger} {*}{
    -body {
        cursor1 seek "1234" "test"
    }
    -returnCodes error
    -match glob
    -result {expected integer*}
}

test unqlite-2.7 {Cursor seek} {*}{
    -body {
        cursor1 seek "1234" 0
    }
    -result {1}
}

test unqlite-2.8 {Cursor isvalid} {*}{
    -body {
        cursor1 isvalid
    }
    -result {1}
}

test unqlite-2.9 {Cursor getkey} {*}{
    -body {
        cursor1 getkey
    }
    -result {1234}
}

test unqlite-2.10 {Cursor getdata} {*}{
    -body {
        cursor1 getdata
    }
    -result {567890}
}

test unqlite-2.11 {Cursor reset} {*}{
    -body {
        cursor1 reset
    }
    -result {1}
}

test unqlite-2.12 {Delete} {*}{
    -body {
        ::db kv_delete "1234"   
    }
    -result {1}
}


#-------------------------------------------------------------------------------

test unqlite-3.1 {random_sting, expected interger} {*}{
    -body {
        ::db random_string "test"
    }
    -returnCodes error
    -match glob
    -result {expected integer*}
}

#-------------------------------------------------------------------------------

catch {cursor1 release}
catch {rename ::db {}}

cleanupTests
return
